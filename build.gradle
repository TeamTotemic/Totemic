buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.9'
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.12.2-0.10.1"
group = "pokefenn.totemic"
archivesBaseName = "Totemic"

minecraft {
    version = "1.12.2-14.23.1.2555"
    runDir = "run"

    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    replaceIn "Totemic.java"
    replace "\${version}", project.version
    replace '@FINGERPRINT@', project.findProperty('signSHA1')
}

sourceCompatibility = targetCompatibility = '1.8'

repositories {
    maven {
        url "http://tehnut.info/maven"
    }
}

dependencies {
    deobfCompile "mcp.mobius.waila:Hwyla:1.8.21-B36_1.12"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output) {
        appendix = 'deobf'
    }
}

// Create API source jar
task apiSrcJar(type: Jar) {
    from(sourceSets.main.java) {
        include 'pokefenn/totemic/api/**'
        appendix = 'api'
    }
    classifier = 'sources'
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    // Skips if the keyStore property is missing.
    onlyIf {
        project.hasProperty('keyStore')
    }

    // findProperty allows us to reference the property without it existing.
    // Using project.propName would cause the script to fail validation if
    // the property did not exist.
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

// Runs this task automatically when reobfJar is ran.
reobfJar.finalizedBy signJar

artifacts {
    archives deobfJar
    archives apiSrcJar
}

curseforge {
    apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

    project {
        id = '237541'
        changelog  = ''
        releaseType = 'alpha'

        addArtifact deobfJar
        addArtifact apiSrcJar
    }
}
